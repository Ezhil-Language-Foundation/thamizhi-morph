### ThamizhiFST-Verb1.foma ###
read lexc ThamizhiVerbs-3-4-62-11-12.lexc

define Lexicon;

define VM [ா | ி | ீ | ு | ூ | ெ |ே | ொ |ோ |ை];
define CON [க | ச | ட | த | ப | ற | ய | ர | ள | வ | ழ | ல | ங | ஞ | ண | ந | ம | ன ];
define longcon [{மீ}|{வே}|{கே}|{தோ}];

######### Rewrite rules

define C1R1 {ய்} -> {ய்ய} || _ "^" [{ா}|{ு}|{அ}];
define C1R2 {ு} -> 0 || _ "^" [{ு}|{ேன்}|{ா}|{அ}];
define C1R3 {ை} -> {ைய} || _ "^" [{ு}|{ேன்}|{ா}];

define C2R1 {ள்} -> 0  || _ "^" [{ண்}];
define C2R2 {ள்} -> {ள}  || _ "^" [{ு}|{அ}|{ா}];
define C2R3 {ள்} -> {ண்}  || _ "^" [{டு}];

define C4R1 {ை} -> {ைய} || _ "^" [{ும்}|{அ}|{ா}];
define C4R2  {்} -> 0  || _ "^" [{ும்}|{ா}];
define C4R3 {ி} -> {ிய} || _ "^" [{ும்}|{ா}];
define C4R4 {ு} -> {ுக்க} || _ "^" [{ும்}|{அ}];
define C4R5 {ழ்} -> {ழ}  || _ "^" [{அ}];
define C4R6 {ி} -> {ிக்க}  || _ "^" [{அ}];
define C4R7 {ர்} -> {ர்க்க} || _ "^" [{அ}];
define C4R8 [{தீ}] -> {தீய} || _ "^" [{அ}|{ா}];
define C4R9 [{ஈ}] -> {ஈய} || _ "^" [{அ}];

define C5Rule1 {ு} -> {ி}  || _ "^" [{ன}];
define C5Rule2 {ு} -> 0  || _ "^" [{ு}];
define C5Rule3 {ு} -> 0  || _ "^" [{ி}];
define C5Rule4 {ு} -> 0  || _ "^" [{ா}|{அ}];

define C61Rule1 {டு} -> 0  || _ "^" [{ட்}];
define C61Rule2 {ு} -> 0  || _ "^" [{ு}|{அ}];
define C61Rule3 {ு} -> {்}  || _ "^" [{டு}];
define C61Rule4 {ு} -> 0  || _ "^" [{ி}];
define C61Rule5 {ு} -> 0  || _ "^" [{ா}];

define C62Rule1 {கு} -> 0  || _ "^" [{க்}];
define C62Rule2 {ு} -> 0  || _ "^" [{ு}];
define C62Rule3 {ு} -> 0  || _ "^" [{ி}];
define C62Rule4 {ு} -> 0  || _ "^" [{ா}];
define C62Rule5 {ு} -> {ுக்க}  || _ "^" [{அ}];

define C63Rule1 {று} -> 0  || _ "^" [{ற்}];
define C63Rule2 {ு} -> 0  || _ "^" [{ு}|{அ}];
define C63Rule3 {ு} -> 0  || _ "^" [{ி}];
define C63Rule4 {ு} -> 0  || _ "^" [{ா}];
define C63Rule5 {ு} -> {்}  || _ "^" [{று}];

define C7Rule1 {உண்} -> {உண்ண}  || _ "^" [{ு} | {ா} | {ி} | {அ}];
define C7Rule2 {பூண்} -> {பூண}  || _ "^" [{ு} | {ா} | {ி}| {அ}];
define C7Rule3 {மாண்} -> {மாண}  || _ "^" [{ு} | {ா} | {ி} | {அ}];
define C7Rule4 {முரண்} -> {முரண}  || _ "^" [{ு} | {ா} | {ி} | {அ}];
define C7Rule5 {ண்} -> 0  || _ "^" [{ளா}];

define C8Rule1 {தின்} -> {தின்ன}  || _ "^" [{ும்}| {ா} | {ி} | {அ}];
define C8Rule2 {நோன்} -> {நோன்ன}  || _ "^" [{ும்}| {ா} | {ி} | {அ}];
define C8Rule3 {ஈன்} -> {ஈன்ன}  || _ "^" [{ும்}| {ா} | {ி} | {அ}];
define C8Rule4 {ஈன்} -> {ஈ}  || _ "^" [{னா}];

define C9Rule1 {ள்} -> {ள}  || _ "^" [{ும்}| {ா} | {ி} | {அ}];
define C9Rule2 {ள்} -> 0  || _ "^" [{ட்}];
define C9Rule3 {ள்} -> 0  || longcon _ "^" [{ளா}];
define C9Rule4 {ள்} -> {ட்}  || _ "^" [{டு}];

define C10Rule1 {ல்} -> 0  || _ "^" [{ற்}];
define C10Rule2 {ல்} -> {ற்க}  || _ "^" [{ும்}| {ா} | {ி} | {அ}];
define C10Rule3 {ல்} -> {ற்}  || _ "^" [{கா} | {று}];

define C13Rule1 {கு} -> {க்க}  || _ "^" [{ி}];
define C13Rule2 {ு} -> 0  || _ "^" [{ு}| {ா} | {ி} |{அ}];

define C14Rule1 {காண்} -> {கண்}  || _ "^" [{ட்}|{டு}|{ட}];
define C14Rule2 {்} -> 0  || _ "^" [{ு}| {ா} | {ி}| {அ}];

define C15Rule1 {சா} -> {செத்}  || _ "^" [{த்}|{த}|{து}];
define C15Rule2 {சா} -> {சாக}  || _ "^" [{ு}| {ா} | {ி}| {அ}];

define C16Rule1 {தா} -> {த}  || _ "^" [{ந்}];
define C16Rule2 {தா} -> {தர}  || _ "^" [{ா} | {ி}| {அ}];
define C16Rule3 {தா} -> {தார}  || _ "^" [{ு}];
define C16Rule4 {தா} -> {தந்}  || _ "^" [{து}];

define C17Rule1 {வா} -> {வ}  || _ "^" [{ந்}];
define C17Rule2 {வா} -> {வர}  || _ "^" [{ா} | {ி}| {அ}];
define C17Rule3 {வா} -> {வார}  || _ "^" [{ு}];
define C17Rule4 {வா} -> {வந்}  || _ "^" [{து}];

define C18Rule1 {வே} -> {வெந்}  || _ "^" [{த்}|{த}|{து}];
define C18Rule2 {வே} -> {வேக}  || _ "^" [{ு}| {ா} | {ி}| {அ}];

define C0Rule1 {ய்} -> {ய்ய}  || _ "^" [ {அ}];

# To Cleanup: remove morpheme boundaries
define Cleanup1 "அ" -> 0 || "^" _;
define Cleanup2 {அப்} -> {ப்} || "^" _;
define Cleanup3 {அச்} -> {ப்} || "^" _;
define Cleanup4 {அத்} -> {ப்} || "^" _;
define Cleanup5 {அக்} -> {ப்} || "^" _;
define Cleanup6 "^" -> 0;


regex	 	Lexicon           	.o.
		  	C1R1	 	.o.
			C1R2	 	.o.
			C1R3	 	.o.
		  	C2R1		.o.
			C2R2		.o.
			C2R3		.o.
			C4R1	.o.
			C4R2	.o.
			C4R3	.o.
			C4R4	.o.
			C4R5	.o.
			C4R6	.o.
			C4R7	.o.
			C4R8	.o.
			C4R9	.o.
			C5Rule1           	.o.
			C5Rule2           	.o.
			C5Rule3           	.o.
			C5Rule4           	.o.
			C61Rule1           	.o.
			C61Rule2           	.o.
			C61Rule3           	.o.
			C61Rule4           	.o.
			C61Rule5           	.o.
			C63Rule1           	.o.
			C63Rule2           	.o.
			C63Rule3           	.o.
			C63Rule4           	.o.
			C63Rule5           	.o.
			C7Rule1		.o.
			C7Rule2		.o.
			C7Rule3		.o.
			C7Rule4		.o.
			C7Rule5		.o.
			C8Rule1           	.o.
			C8Rule2           	.o.
			C8Rule3           	.o.
			C8Rule4           	.o.
			C9Rule1           	.o.
			C9Rule2           	.o.
			C9Rule3           	.o.
			C9Rule4           	.o.
			C10Rule1           	.o.
			C10Rule2           	.o.
			C10Rule3           	.o.
			C13Rule1           	.o.
			C13Rule2           	.o.
			C14Rule1           	.o.
			C14Rule2           	.o.
			C15Rule1           	.o.
			C15Rule2           	.o.
			C16Rule1           	.o.
			C16Rule2           	.o.
			C16Rule3           	.o.
			C16Rule4           	.o.
			C17Rule1           	.o.
			C17Rule2           	.o.
			C17Rule3           	.o.
			C17Rule4           	.o.
			C18Rule1           	.o.
			C18Rule2           	.o.
			C0Rule1			.o.
			Cleanup1		.o.
			Cleanup2		.o.
			Cleanup3		.o.
			Cleanup4		.o.
			Cleanup5		.o.
			Cleanup6;

