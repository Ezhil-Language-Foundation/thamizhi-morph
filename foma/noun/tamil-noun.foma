### Tamil.foma ###

define LongChar [ா|ீ|ூ|ை|ோ|ே|ௌ|ஆ];
define CharSet1 [ய|ர];
define CharSet2 [த|க|ம|வ|ண|ள|ன|ல|ர];


#read lexc tamil-noun.lexc
read lexc Nouns.lexc

define Lexicon;

define R1 {்} -> 0 || LongChar CharSet1 _ "^" [{ி}|{ு}|{ோ}|{து}|{ை}|{ா}];
define R101 {ய்} -> 0 || LongChar  _ "^" [{ோ}];
define R2 {்} -> 0 || CharSet2 _ "^" [{ி}|{ு}|{ோ}|{து}|{ை}|{ா}];
define R3 {டு} -> 0 ||  _ "^" {ட்ட};   #For Kaadu class
define R4 {ு} -> 0 || _ "^" {ஆ}; #For Vandu
define R5 {று} -> 0 || _ "^" {ற}; #For Aaru
define R19 {ன்}-> 0 ||  _ "^" [{ற்த்}|{ற்ச்}|{ற்ட்}|{ற்ப்}]; #For பொற்  
define R6 {ன்} -> 0 || _ "^" [{ர்க}]; #For Manavan
define R7 {ன்} -> 0 || _ "^" [{னி}|{னோ}|{னா}|{னு}]; #For Manavan
define R18 {ன்}-> 0 ||  _ "^" [{த்}|{ச்}|{ட்}|{ப்}]; #For மாணவப் 
define R8 {ல்} -> 0 || _ "^" [{ற்க}]; #For புல்
define R9 {ள்} -> 0 || _ "^" [{ட்க}]; #For முள்
define R10 {ள்} -> 0 || LongChar _ "^" [{ட்க}]; #For நாள்
define R11 {்} -> 0 || {ருள} _ "^" [{ி}|{ு}|{ோ}|{ை}|{ா}]; #For அருள்
define R12 {ம்}-> 0 ||  _ "^" [{ங்க}]; #For மரங்கள்
define R17 {ம்}-> 0 ||  _ "^" [{த்}|{ச்}|{ட்}|{ப்}]; #For மரச் 
define R13 {ம்} -> 0 ||  _ "^" [{த்த}]; #For மரம் - மரத்தை
define R14 {ர்} -> 0 ||  _ "^" [{ற்ற}]; #For சுவர் - சுவற்றில்
define R15 {்} -> 0 || {ள} _ "^" [{ி}|{ு}|{ோ}|{ை}|{ா}]; 
define R16 {ன்} -> 0 || _ "^" [{ற்}]; #பொன் - பொற்


define PR1 "நம்" -> 0 ||  _ "^" [{ந}];    #நம் 
define PR2 {தாங்கள்} -> 0,{தாங்கள்} -> 0 ||  _ "^" [{த}];    
define PR3 {எங்கள்} -> 0,{எம்} -> 0,{என்} -> 0 ||  _ "^"  [{எ}];   
define PR4 {அவன்} -> 0,{அவள்} -> 0,{அது} -> 0,{அவை} -> 0,{அவர்} -> 0,{அவர்கள்} -> 0 ||  _ "^"  [{அ}];    
define PR5 {உவன்} -> 0,{உவள்} -> 0,{உது} -> 0,{உவை} -> 0,{உன்} -> 0,{உம்} -> 0,{உங்கள்} -> 0,{உவர்} -> 0,{உவர்கள்} -> 0 ||  _ "^"  [{உ}];    
define PR6 {இவன்} -> 0,{இவள்} -> 0,{இது} -> 0,{இவை} -> 0,{இவர்} -> 0,{இவர்கள்} -> 0 ||  _ "^"  [{இ}];   


define Cleanup1 "ஆ" -> 0 || "^" _;
define CleanupX "^" -> 0;


define TamilNouns		 	Lexicon           	.o.
						R1				.o.
						R101				.o.
						R2				.o.
						R3				.o.
						R4				.o.
						R5				.o.	
						R6				.o.
						R7				.o.						
						R8				.o.	
						R9				.o.		
						R10				.o.	
						R11				.o.		
						R12				.o.	
						R13				.o.			
						R14				.o.	
						R15				.o.	
						R16				.o.	
						R17				.o.
						R18				.o.
						R19				.o.						
						PR1          .o.
						PR2          .o.
						PR3          .o.
						PR4          .o.
						PR5          .o.
						PR6          .o.
						Cleanup1			.o.			
						CleanupX;

regex TamilNouns;

